@top file { line* }
line {
    statement (lf | eof)
    | lf
}

@precedence { more, not, quantified, or @right }
@skip { ws | Comment }
@detectDelim

Chars                   { (!more (char | literalLhs | Escape))+ }
rhsChars[@name=Chars]   { (!more (char | literalRhs | Escape))+ }
literalLhs {
    "{}" | "∅"
}
literalRhs {
    "@" | "%" | "=" | "/" | "_" | "(" | ")" | "[" | "]" | "{" | "}" | "<" | ">" | "~" | "|" | "+" | "*" | "->" | "→"
}

statement {
    Option      { "@" Chars Eq Chars }
    | Template  { "%" Chars Eq lhs }
    | Rule      { Slash? Lookaround? lhs Lookaround? Slash? Arrow rhs When? }
}
Lookaround { "[" lhs "]" }
When { "?" Chars }

lhs  { expr+ }
expr {
    Group               { "(" expr ")" }
    | Not               { "~" expr !not }
    | quantification    { expr !quantified Quantifier }
    | Or                { expr !or "|" expr }
    | OrGroup           { "<" Chars ">" }
    | Substitution      { "{" Chars "}" }
    | Underscore        { "_" }
    | Chars
}
Quantifier      { "?" | "+" | "*" }

rhs {
    Void        { "{}" | "∅" }
    | rhsChars
}

@tokens {
    eof         { @eof }
    lf          { ("\r"? "\n")+ }
    ws          { @whitespace+ }
    Comment     { "#" ![\n]* }
    Eq          { "=" }
    Slash       { "/" }
    Arrow       { "->" | "→" }
    Escape {
        "\\u" $[0-9A-Fa-f] $[0-9A-Fa-f] $[0-9A-Fa-f] $[0-9A-Fa-f]
        | "\\" _
    }
    char { ![#@%=→∅()[\]{}<>~|/_?+*] }
    @precedence { lf, ws, char }
}
