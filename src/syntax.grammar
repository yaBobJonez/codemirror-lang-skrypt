@top file { line* }
line {
    statement (lf | eof)
    | lf
}

@precedence { more, not, quantified, or @right }
@skip { ws | Comment }
@detectDelim

Chars                   { (!more (char | literalLhs | Escape))+ }
rhsChars[@name=Chars]   { (!more (char | literalRhs | Escape))+ }
literalLhs {
    "{}" | "∅"
}
literalRhs {
    "@" | "%" | "="
    | "," | "/" | "_" | "->" | "→"
    | "(" | ")" | "[" | "]" | "{" | "}" | "<" | ">"
    | "~" | "|" | "+" | "*"
}

statement {
    Option      { "@" Chars Eq Chars }
    | Template  { "%" Chars Eq lhs }
    | Rule      { lhs Arrow rhs When? }
}
When { "?" Chars }

lhs     { pattern (Comma lf? pattern)* }
pattern { Slash? Lookaround? expr Lookaround? Slash? }
expr    {
    terms   { term+ }
    | Or    { expr !or "|" expr }
}
term {
    Group               { "(" expr ")" }
    | Not               { "~" term !not }
    | quantification    { term !quantified Quantifier }
    | OrGroup           { "<" Chars ">" }
    | Substitution      { "{" Chars "}" }
    | Underscore        { "_" }
    | Chars
}
Lookaround  { "[" expr "]" }
Quantifier  { "?" | "+" | "*" }

rhs {
    Void        { "{}" | "∅" }
    | rhsChars
}

@tokens {
    eof         { @eof }
    lf          { ("\r"? "\n")+ }
    ws          { @whitespace+ }
    Comment     { "#" ![\n]* }
    Eq          { "=" }
    Comma       { "," }
    Slash       { "/" }
    Arrow       { "->" | "→" }
    Escape {
        "\\u" $[0-9A-Fa-f] $[0-9A-Fa-f] $[0-9A-Fa-f] $[0-9A-Fa-f]
        | "\\" _
    }
    char { ![#@%=,→∅()[\]{}<>~|/_?+*] }
    @precedence { lf, ws, char }
}
